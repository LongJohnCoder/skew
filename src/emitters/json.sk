export namespace json {
  export string dump(Node node) {
    var visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.result;
  }

  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = [];
      if (options.outputDirectory == "") {
        outputs.push(Source(options.outputFile, dump(program) + "\n"));
      } else {
        for (var i = 0; i < program.children.size(); i++) {
          var file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name + ".json"), dump(file) + "\n"));
        }
      }
      return outputs;
    }
  }

  class DumpVisitor {
    var result = "";
    var indent = "";

    void visit(Node node) {
      if (node == null) {
        result += "null";
        return;
      }
      var outer = indent;
      indent += "  ";
      result += "{\n" + indent + "\"kind\": " + simpleQuote(node.kind.prettyPrint());
      if (node.content != null) {
        result += ",\n" + indent + "\"content\": ";
        switch (node.content.type()) {
          case .INT { result += node.asInt(); }
          case .BOOL { result += node.asBool(); }
          case .DOUBLE { result += node.asDouble(); }
          case .STRING { result += quoteString(node.asString(), '"'); }
        }
      }
      if (node.hasChildren()) {
        result += ",\n" + indent + "\"children\": [";
        var inner = indent;
        indent += "  ";
        for (var i = 0; i < node.children.size(); i++) {
          if (i > 0) result += ",";
          result += "\n" + indent;
          visit(node.children.get(i));
        }
        indent = inner;
        result += "\n" + indent + "]";
      }
      indent = outer;
      result += "\n" + indent + "}";
    }
  }
}
