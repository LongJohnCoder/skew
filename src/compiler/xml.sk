namespace xml {
  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = {};
      if (options.outputDirectory == "") {
        outputs.push(Source(options.outputFile, dump(program) + "\n"));
      } else {
        for (var i = 0; i < program.children.length; i++) {
          var file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name + ".xml"), dump(file) + "\n"));
        }
      }
      return outputs;
    }
  }

  string dump(Node node) {
    var visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.result;
  }

  class DumpVisitor {
    var result = "";
    var indent = "";

    void visit(Node node) {
      if (node == null) {
        result += "<null/>";
        return;
      }
      result += "<" + node.kind.prettyPrint();
      if (node.content != null) {
        result += " content=";
        switch (node.content.type()) {
          case .INT { result += simpleQuote(node.asInt().toString()); }
          case .DOUBLE { result += simpleQuote(node.asDouble().toString()); }
          case .STRING { result += quoteString(node.asString(), '"'); }
        }
      }
      if (node.hasChildren()) {
        result += ">";
        var inner = indent;
        indent += "  ";
        for (var i = 0; i < node.children.length; i++) {
          result += "\n" + indent;
          visit(node.children.get(i));
        }
        indent = inner;
        result += "\n" + indent + "</" + node.kind.prettyPrint() + ">";
      } else {
        result += "/>";
      }
    }
  }
}
