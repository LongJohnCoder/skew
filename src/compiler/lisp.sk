namespace lisp {
  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = {};
      if (options.outputDirectory == "") {
        outputs.push(Source(options.outputFile, dump(program) + "\n"));
      } else {
        for (var i = 0; i < program.children.length; i++) {
          var file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name + ".lisp"), dump(file) + "\n"));
        }
      }
      return outputs;
    }
  }

  string dump(Node node) {
    var visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.result;
  }

  class DumpVisitor {
    var result = "";
    var indent = "";

    void visit(Node node) {
      if (node == null) {
        result = result + "nil";
        return;
      }
      result = result + "(" + node.kind.prettyPrint();
      if (node.content != null) {
        switch (node.content.type()) {
          case .INT { result = result + " " + node.asInt(); }
          case .DOUBLE { result = result + " " + node.asDouble(); }
          case .STRING { result = result + " " + quoteString(node.asString(), '"'); }
        }
      }
      if (node.hasChildren()) {
        var old = indent;
        indent = indent + "  ";
        for (var i = 0; i < node.children.length; i++) {
          result = result + "\n" + indent;
          visit(node.children.get(i));
        }
        indent = old;
      }
      result = result + ")";
    }
  }
}
