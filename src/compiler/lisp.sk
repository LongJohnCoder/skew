namespace lisp {
  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = {};
      if (options.isSingleFileMode()) {
        outputs.push(Source(options.outputFile, dump(program).append("\n")));
      } else {
        int i;
        for (i = 0; i < program.children.length; i++) {
          Node file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name.append(".lisp")), dump(file).append("\n")));
        }
      }
      return outputs;
    }
  }

  string dump(Node node) {
    DumpVisitor visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.builder.toString();
  }

  class DumpVisitor {
    StringBuilder builder = StringBuilder();
    string indent = "";

    void visit(Node node) {
      if (node == null) {
        builder.append("nil");
        return;
      }
      builder.append("(").append(node.kind.prettyPrint());
      if (node.content != null) {
        switch (node.content.type()) {
          case .INT { builder.append(" ").append(node.asInt().toString()); }
          case .DOUBLE { builder.append(" ").append(node.asDouble().toString()); }
          case .STRING { builder.append(" ").append(quoteString(node.asString(), '"')); }
        }
      }
      if (node.hasChildren()) {
        string old = indent;
        indent = indent.append("  ");
        int i;
        for (i = 0; i < node.children.length; i++) {
          builder.append("\n").append(indent);
          visit(node.children.get(i));
        }
        indent = old;
      }
      builder.append(")");
    }
  }
}
