interface IEmitter {
  virtual List<Source> emitProgram(Node program);
}

export enum TargetFormat {
  // Target languages that run the resolver
  NONE,
  CPP,
  JAVASCRIPT,

  // AST-only targets
  LISP_AST,
  JSON_AST,
  XML_AST,
}

export in TargetFormat {
  bool shouldRunResolver() {
    return this >= NONE && this <= JAVASCRIPT;
  }
}

export class CompilerOptions {
  export TargetFormat targetFormat = .NONE;
  export List<Source> inputs = [];
  export List<Source> prepend = [];
  export List<Source> append = [];
  export var outputDirectory = "";
  export var outputFile = "";
  export var jsMinify = false; // Removes whitespace
  export var jsMangle = false; // Changes the code structure to save space
  export var jsSourceMap = false;
  export var removeAsserts = false;
  export var foldAllConstants = false;
  export var inlineAllFunctions = false;
  export var convertAllInstanceToStatic = false;
}

export class CompilerResult {
  export CompilerOptions options;
  export List<Source> outputs;
  export Node program;
  export Resolver resolver;
}

class CachedSource {
  string contents;
  Source source = null;
  Node file = null;

  Node compile(Compiler compiler, CompilerOptions options) {
    if (file == null) {
      var program = Node.createProgram([]);
      source = Source("<native>", contents);
      compiler.processInput(program, source);
      assert program.children.size() > 0;
      file = program.children.get(0);
    }
    options.inputs.unshift(source);
    return file.clone();
  }
}

export class Compiler {
  // Cache the standard library for a small speed boost
  static CachedSource nativeLibrary = CachedSource(NATIVE_LIBRARY);
  static CachedSource nativeLibraryJS = CachedSource(NATIVE_LIBRARY_JS);
  static CachedSource nativeLibraryCPP = CachedSource(NATIVE_LIBRARY_CPP);

  var tokenizingTime = 0.0;
  var parsingTime = 0.0;
  var resolvingTime = 0.0;
  var callGraphTime = 0.0;
  var instanceToStaticTime = 0.0;
  var symbolMotionTime = 0.0;
  var functionInliningTime = 0.0;
  var constantFoldingTime = 0.0;
  var deadCodeRemovalTime = 0.0;
  var emitTime = 0.0;
  var lineCountingTime = 0.0;
  var totalTime = 0.0;
  var log = Log();

  string statistics(CompilerResult result) {
    // Line count
    var lineCountingStart = now();
    var lineCount = 0;
    lineCount += totalLineCount(result.options.prepend);
    lineCount += totalLineCount(result.options.inputs);
    lineCount += totalLineCount(result.options.append);
    var text =
      "Input line count: " + lineCount +
      "\nOutput line count: " + totalLineCount(result.outputs);
    lineCountingTime += now() - lineCountingStart;

    // Compilation time
    var optimizingTime = callGraphTime + instanceToStaticTime + symbolMotionTime + functionInliningTime + constantFoldingTime + deadCodeRemovalTime;
    text += "\nTotal compile time: " + formatPositiveNumber(totalTime + lineCountingTime) + "ms";
    if (tokenizingTime > 0) text += "\n  Tokenizing: " + formatPositiveNumber(tokenizingTime) + "ms";
    if (parsingTime > 0) text += "\n  Parsing: " + formatPositiveNumber(parsingTime) + "ms";
    if (resolvingTime > 0) text += "\n  Resolving: " + formatPositiveNumber(resolvingTime) + "ms";
    if (optimizingTime > 0) {
      text += "\n  Optimizing: " + formatPositiveNumber(optimizingTime) + "ms";
      text += "\n    Building call graph: " + formatPositiveNumber(callGraphTime) + "ms";
      text += "\n    Instance to static: " + formatPositiveNumber(instanceToStaticTime) + "ms";
      text += "\n    Symbol motion: " + formatPositiveNumber(symbolMotionTime) + "ms";
      text += "\n    Function inlining: " + formatPositiveNumber(functionInliningTime) + "ms";
      text += "\n    Constant folding: " + formatPositiveNumber(constantFoldingTime) + "ms";
      text += "\n    Dead code removal: " + formatPositiveNumber(deadCodeRemovalTime) + "ms";
    }
    if (emitTime > 0) text += "\n  Emit: " + formatPositiveNumber(emitTime) + "ms";
    if (lineCountingTime > 0) text += "\n  Counting lines: " + formatPositiveNumber(lineCountingTime) + "ms";

    // Sources
    text += sourceStatistics("Prepend", result.options.prepend);
    text += sourceStatistics("Inputs", result.options.inputs);
    text += sourceStatistics("Append", result.options.append);
    text += sourceStatistics("Outputs", result.outputs);
    return text;
  }

  static int totalLineCount(List<Source> sources) {
    var lineCount = 0;
    for (var i = 0; i < sources.size(); i++) {
      lineCount += sources.get(i).lineCount();
    }
    return lineCount;
  }

  static string sourceStatistics(string name, List<Source> sources) {
    var total = 0;
    for (var i = 0; i < sources.size(); i++) {
      total += sources.get(i).contents.size();
    }
    var text = "\n" + name + ": " + sources.size() + " (" + bytesToString(total) + " total)";
    for (var i = 0; i < sources.size(); i++) {
      var source = sources.get(i);
      text += "\n  " + source.name + ": " + bytesToString(source.contents.size());
    }
    return text;
  }

  export CompilerResult compile(CompilerOptions options) {
    var totalStart = now();
    var program = Node.createProgram([]);
    List<Source> outputs = [];

    // Initialize global state
    createOperatorMap();
    createParser();
    createNameToSymbolFlag();
    createSymbolFlagToName();

    // Tokenize and parse each module individually
    switch (options.targetFormat) {
      case .JAVASCRIPT { program.appendChild(nativeLibraryJS.compile(this, options)); }
      case .CPP { program.appendChild(nativeLibraryCPP.compile(this, options)); }
      default { program.appendChild(nativeLibrary.compile(this, options)); }
    }
    for (var i = 1; i < options.inputs.size(); i++) {
      processInput(program, options.inputs.get(i));
    }

    // Resolve everything together
    Resolver resolver = null;
    if (options.targetFormat.shouldRunResolver()) {
      var resolveStart = now();
      resolver = Resolver(log, options);
      resolver.run(program);
      resolvingTime += now() - resolveStart;
    }
    if (log.errorCount == 0) {

      // Optimize the module if the target format requires it
      if (options.targetFormat.shouldRunResolver()) {
        // Build a call graph
        var callGraphStart = now();
        var graph = CallGraph(program);
        callGraphTime += now() - callGraphStart;

        // Convert instance functions to static functions
        var instanceToStaticStart = now();
        InstanceToStaticPass.run(graph, resolver);
        instanceToStaticTime += now() - instanceToStaticStart;

        // Move functions
        var symbolMotionStart = now();
        SymbolMotionPass.run(resolver);
        symbolMotionTime += now() - symbolMotionStart;

        // Inline functions
        var functionInliningStart = now();
        FunctionInliningPass.run(graph, options);
        functionInliningTime += now() - functionInliningStart;

        // Fold constants
        if (options.foldAllConstants) {
          var constantFoldingStart = now();
          resolver.constantFolder.foldConstants(program);
          constantFoldingTime += now() - constantFoldingStart;
        }

        // Dead code elimination
        var deadCodeRemovalStart = now();
        DeadCodeRemovalPass.run(program, options, resolver);
        deadCodeRemovalTime += now() - deadCodeRemovalStart;
      }

      // Emit the module in the target format
      IEmitter emitter = null;
      switch (options.targetFormat) {
        case .NONE {}
        case .CPP { emitter = cpp.Emitter(resolver); }
        case .JAVASCRIPT { emitter = js.Emitter(resolver); }
        case .LISP_AST { emitter = lisp.Emitter(options); }
        case .JSON_AST { emitter = json.Emitter(options); }
        case .XML_AST { emitter = xml.Emitter(options); }
        default { assert false; }
      }
      if (emitter != null) {
        var emitStart = now();
        outputs = emitter.emitProgram(program);
        emitTime += now() - emitStart;
      }
    }

    totalTime += now() - totalStart;
    return CompilerResult(options, outputs, program, resolver);
  }

  void processInput(Node program, Source source) {
    var errorCount = log.errorCount;

    // Tokenize the source
    var tokenizeStart = now();
    var sourceTokens = tokenize(log, source);
    tokenizingTime += now() - tokenizeStart;

    // Parse the source if the tokenize was successful
    if (log.errorCount == errorCount) {
      var parseStart = now();
      var file = parseFile(log, sourceTokens);
      parsingTime += now() - parseStart;
      if (file != null) {
        program.appendChild(file);
      }
    }
  }
}
