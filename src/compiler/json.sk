namespace json {
  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = {};
      if (options.isSingleFileMode()) {
        outputs.push(Source(options.outputFile, dump(program).append("\n")));
      } else {
        for (var i = 0; i < program.children.length; i++) {
          var file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name.append(".json")), dump(file).append("\n")));
        }
      }
      return outputs;
    }
  }

  string dump(Node node) {
    var visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.builder.toString();
  }

  class DumpVisitor {
    var builder = StringBuilder();
    var indent = "";

    void visit(Node node) {
      if (node == null) {
        builder.append("null");
        return;
      }
      var outer = indent;
      indent = indent.append("  ");
      builder.append("{\n").append(indent).append("\"kind\": ").append(simpleQuote(node.kind.prettyPrint()));
      if (node.content != null) {
        builder.append(",\n").append(indent).append("\"content\": ");
        switch (node.content.type()) {
          case .INT { builder.append(node.asInt().toString()); }
          case .DOUBLE { builder.append(node.asDouble().toString()); }
          case .STRING { builder.append(quoteString(node.asString(), '"')); }
        }
      }
      if (node.hasChildren()) {
        builder.append(",\n").append(indent).append("\"children\": [");
        var inner = indent;
        indent = indent.append("  ");
        for (var i = 0; i < node.children.length; i++) {
          if (i > 0) builder.append(",");
          builder.append("\n").append(indent);
          visit(node.children.get(i));
        }
        indent = inner;
        builder.append("\n").append(indent).append("]");
      }
      indent = outer;
      builder.append("\n").append(indent).append("}");
    }
  }
}
