namespace json {
  class Emitter : IEmitter {
    CompilerOptions options;

    override List<Source> emitProgram(Node program) {
      List<Source> outputs = {};
      if (options.isSingleFileMode()) {
        outputs.push(Source(options.outputFile, dump(program).append("\n")));
      } else {
        for (var i = 0; i < program.children.length; i++) {
          var file = program.children.get(i);
          outputs.push(Source(joinPath(options.outputDirectory, file.range.source.name.append(".json")), dump(file).append("\n")));
        }
      }
      return outputs;
    }
  }

  string dump(Node node) {
    var visitor = DumpVisitor();
    visitor.visit(node);
    return visitor.result;
  }

  class DumpVisitor {
    var result = "";
    var indent = "";

    void visit(Node node) {
      if (node == null) {
        result = result.append("null");
        return;
      }
      var outer = indent;
      indent = indent.append("  ");
      result = result.append("{\n").append(indent).append("\"kind\": ").append(simpleQuote(node.kind.prettyPrint()));
      if (node.content != null) {
        result = result.append(",\n").append(indent).append("\"content\": ");
        switch (node.content.type()) {
          case .INT { result = result.append(node.asInt().toString()); }
          case .DOUBLE { result = result.append(node.asDouble().toString()); }
          case .STRING { result = result.append(quoteString(node.asString(), '"')); }
        }
      }
      if (node.hasChildren()) {
        result = result.append(",\n").append(indent).append("\"children\": [");
        var inner = indent;
        indent = indent.append("  ");
        for (var i = 0; i < node.children.length; i++) {
          if (i > 0) result = result.append(",");
          result = result.append("\n").append(indent);
          visit(node.children.get(i));
        }
        indent = inner;
        result = result.append("\n").append(indent).append("]");
      }
      indent = outer;
      result = result.append("\n").append(indent).append("}");
    }
  }
}
